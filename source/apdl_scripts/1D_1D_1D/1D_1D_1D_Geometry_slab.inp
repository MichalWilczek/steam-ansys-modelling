
winding_counter = 1
reel_number = 1
is_reel_number_odd = mod(reel_number, 2)
kp_per_winding = division_per_winding+1
ins_elem_number = number_of_windings*2+1

x=0
y=0
z=0

*do,a,1,number_of_windings/number_of_windings_in_reel
  *do,b,1,number_of_windings_in_reel
    allsel
    *get,kp_first_winding_i,kp,,count
    kp_first_winding_i = kp_first_winding_i + 1
    *do,c,1,kp_per_winding,1
      allsel
      *get,knum,kp,,count
        k,knum+1,(c-1)*(length_per_winding)/(division_per_winding),y,z
    *enddo
    allsel
    *get,kp_last_winding_i,kp,,count
    allsel
    *get,knum,kp,,count
    line_counter = knum - division_per_winding
    type,winding_counter
    real,winding_counter
    mat,winding_counter
    *do,k,line_counter,line_counter+division_per_winding-1,1
      l,k,k+1,1
      allsel
      *get,lnum,line,,count
      lmesh,lnum
    *enddo
    allsel
    *get,lnum,line,,count
    lsel,s,line,,lnum+1-division_per_winding,lnum
    allsel,below,line
    cm,winding%winding_counter%,line

    !!!!!!!!!!!!!!!!!!!!!!!!!
    !!! CREATE INSULATION !!!
    !!!!!!!!!!!!!!!!!!!!!!!!!
    *if,insulation_analysis,eq,1,then

      *dim,insulation_tip_nodes_array%winding_counter%,array,kp_per_winding*4,1,1
      node_ins_tip_counter = 1

      ! get number of keypoints in one
      cmsel,s,winding%winding_counter%
      allsel,below,line
      *get,kps_winding,kp,,count

      ! get maximum keypoint number in the given winding
      cmsel,s,winding%winding_counter%
      allsel,below,line
      *get,kps_max_winding,kp,,num,max
      *get,kps_min_winding,kp,,num,min

      !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
      !!!! k is creates four sides of the insulation layer !!!!!!!!!!!!!!!!!!!!
      !!!! j is creates the longitudinal elements of each insulation layer !!!!
      !!!! i creates each layer element separately !!!!!!!!!!!!!!!!!!!!!!!!!!!!
      !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

      *do,k,1,4,1

        x_insulation = 0
        y_insulation = 0
        z_insulation = 0

        *do,j,1,trans_division_insulation,1

          *do,i,kps_min_winding,kps_max_winding,1

            allsel
            *get,knum,kp,,count

            *if,j,eq,1,then
              *get,kposx,kp,i,loc,x
              *get,kposy,kp,i,loc,y
              *get,kposz,kp,i,loc,z
            *else
              *get,kposx,kp,knum+1-kp_per_winding,loc,x
              *get,kposy,kp,knum+1-kp_per_winding,loc,y
              *get,kposz,kp,knum+1-kp_per_winding,loc,z
            *endif

            *if,k,eq,1,then
              y_insulation = + eq_trans_dimension_insulation/trans_division_insulation
            *elseif,k,eq,2,then
              y_insulation = - eq_trans_dimension_insulation/trans_division_insulation
            *elseif,k,eq,3,then
              z_insulation = + eq_trans_dimension_insulation/trans_division_insulation
            *elseif,k,eq,4,then
              z_insulation = - eq_trans_dimension_insulation/trans_division_insulation
            *endif

            k,knum+1,kposx+x_insulation,kposy+y_insulation,kposz+z_insulation

            *if,j,eq,1,then
              l,i,knum+1,1
            *else
              l,knum+1-kp_per_winding,knum+1,1
            *endif

			! application of corner nodes
			*if,i,eq,kps_min_winding,or,i,eq,kps_max_winding,then
				type,ins_elem_number+1
				real,ins_elem_number+1
				mat,ins_elem_number+1
			*else
				type,ins_elem_number
				real,ins_elem_number
				mat,ins_elem_number
			*endif
			
            allsel
            *get,lnum,line,,count
            lmesh,lnum

            !!! get the tip node number for further coupling purposes
            *if,j,eq,trans_division_insulation,then
              ksel,s,kp,,knum+1
              nslk,s
              *get,num_node,node,,num,max
              insulation_tip_nodes_array%winding_counter%(node_ins_tip_counter) = num_node
              node_ins_tip_counter = node_ins_tip_counter + 1
            *endif

          *enddo

          allsel
          *get,enum,elem,,count
          esel,s,elem,,enum-division_per_winding,enum
          cm,insulation_layer%j%_side%k%,elem

        *enddo
      *enddo

      ! create named components for windings with insulation
      *do,m,1,4,1
        *do,l,1,trans_division_insulation,1
          *if,m,eq,1,and,l,eq,1,then
            cmsel,s,insulation_layer%l%_side%m%
          *else
            cmsel,a,insulation_layer%l%_side%m%
          *endif
        *enddo
      *enddo
      cm,winding_insulation%winding_counter%,elem

      cmsel,s,winding%winding_counter%
      allsel,below,line
      cmsel,a,winding_insulation%winding_counter%
      cm,winding_with_insulation%winding_counter%,elem
      allsel,below,elem
      cm,winding_with_insulation_nodes%winding_counter%,node

    *endif

    x = x
    *if,is_reel_number_odd,ne,0,then
      y = y + trans_dimension_winding
    *else
      y = y - trans_dimension_winding
    *endif
    z = z
    winding_counter = winding_counter + 1
  *enddo

  ! winding_counter = winding_counter + 1
  reel_number = reel_number + 1
  is_reel_number_odd = mod(reel_number, 2)

  *if,is_reel_number_odd,ne,0,then
    y = 0
  *else
    y = y - trans_dimension_winding
  *endif
  z = z + trans_dimension_winding
*enddo

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!! Coupling algorithm for the insulation nodes !!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

*if,insulation_analysis,eq,1,then
  allsel
  tolerance_range = trans_dimension_winding - 2*eq_trans_dimension_insulation
  *do,j,1,number_of_windings,2
    *do,i,1,kp_per_winding*4,1,1
      nnode = insulation_tip_nodes_array%j%(i)

      csys,0
      allsel
      *get,nposx,node,nnode,loc,x
      *get,nposy,node,nnode,loc,y
      *get,nposz,node,nnode,loc,z

      clocal,20,sphe,nposx,nposy,nposz
      csys,20
      nsel,s,loc,x,tolerance_range
      cm,cur_node_set,node
      cmsel,u,winding_with_insulation_nodes%j%

      *get,num_node,node,,count

      *if,num_node,eq,1,then
        *get,num_node,node,,num,max
        allsel
        nsel,s,node,,num_node
        nsel,a,node,,nnode
        cp,next,temp,all
      *endif
    *enddo
  *enddo
*endif

! definition of winding nodes without insulation
*do,i,1,number_of_windings
  *dim,node_list_winding_%i%,array,division_per_winding+1,1
  cmsel,s,winding%i%
  allsel,below,line
  *vget,node_list_winding_%i%,node,,nlist
  *mwrite,node_list_winding_%i%,Nodes_winding_without_insul_%i%,txt
  (1E20.10)
*enddo

! definition of winding nodes with insulation
*if,insulation_analysis,eq,1,then
  *do,i,1,number_of_windings
    cmsel,s,winding_with_insulation%i%
    allsel,below,elem
    *get,node_wind_ins_count,node,,count
    *dim,node_list_winding_ins%i%,array,node_wind_ins_count,1
    *vget,node_list_winding_ins%i%,node,,nlist
    *mwrite,node_list_winding_ins%i%,Nodes_winding_with_insul_%i%,txt
    (1E20.10)
  *enddo
*endif

! definition of node position in coordinate system
csys,0
allsel
*get,nnum_windings,node,,count
*dim,node_loc_windings,array,nnum_windings,4
*vget,node_loc_windings(1,1),node,,nlist
*vget,node_loc_windings(1,2),node,,loc,x
*vget,node_loc_windings(1,3),node,,loc,y
*vget,node_loc_windings(1,4),node,,loc,z
*mwrite,node_loc_windings,Node_Position,txt
(4E20.10)

*do,i,1,number_of_windings
  *if,i,eq,1,then
    cmsel,s,winding%i%
  *else
    cmsel,a,winding%i%
  *endif
  allsel,below,elem
  *get,nnode,node,,count
*enddo

! definition of nodes in every plane
tolerance_range = 1e-8
*do,i,1,kp_per_winding,1
  nsel,s,node,,i
  *get,node_num,node,0,num,max
  *get,node_num_locx,node,node_num,loc,x
  nsel,s,loc,x,node_num_locx-tolerance_range,node_num_locx+tolerance_range
  *get,node_plane_count,node,,count
  *dim,node_list_plane%i%,array,node_plane_count,1
  *vget,node_list_plane%i%,node,,nlist
  *mwrite,node_list_plane%i%,Nodes_plane_%i%,txt
  (1E20.10)
*enddo

! definition of coil domain 
*do,i,1,number_of_windings
	*if,i,eq,1,then
	    cmsel,s,winding_with_insulation%i%
	*else
		cmsel,a,winding_with_insulation%i%
	*endif
	cm,magnet_coil,elem
*enddo

*cfopen,Nnode,txt
*vwrite,nnode
(1(ES16.7))
*cfclose

*cfopen,Process_Finished,txt
*vwrite,1
(1(ES16.7))
*cfclose
