/clear
/prep7

et,1,plane223 ! coil
et,2,plane223 ! insulation
number_windings = 3
max_nodes_cross_section = 5

length = 500            ! mm
winding_width = 10      ! mm
insulation_width = 1    ! mm

length_division = 100
insulation_division = 3
winding_division = 2

! geometry creation
x=0
y=0
z=0
*do,i,1,number_windings
  allsel
  *get,knum,kp,,count
  k,knum+1,x,y,z
  k,knum+2,x+length,y,z
  k,knum+3,x+length,y+winding_width,z
  k,knum+4,x,y+winding_width,z
  l,knum+1,knum+2,length_division
  l,knum+2,knum+3,winding_division
  l,knum+3,knum+4,length_division
  l,knum+4,knum+1,winding_division
  *get,lnum,line,,count
  lsel,s,line,,lnum-3,lnum
  al,all
  allsel
  *get,anum,area,,count
  asel,s,area,,anum
  cm,winding%i%,area
  mshape,0,2D
  mshkey,1
  type,1
  real,1
  mat,1
  amesh,all

  allsel
  *get,knum,kp,,count
  k,knum+1,x,y+winding_width,z
  k,knum+2,x+length,y+winding_width,z
  k,knum+3,x+length,y+winding_width+insulation_width,z
  k,knum+4,x,y+winding_width+insulation_width,z
  l,knum+1,knum+2,length_division
  l,knum+2,knum+3,insulation_division
  l,knum+3,knum+4,length_division
  l,knum+4,knum+1,insulation_division
  *get,lnum,line,,count
  lsel,s,line,,lnum-3,lnum
  al,all
  allsel
  *get,anum,area,,count
  asel,s,area,,anum
  cm,insulation%i%,area
  mshape,0,2D
  mshkey,1
  type,2
  real,2
  mat,2
  amesh,all

  x = x
  y = y + winding_width + insulation_width
  z = z
*enddo

! set tolerance for finding nodes
tolerance = (length/(length_division*2))/10

*do,i,1,number_windings
  *dim,node_list_winding_%i%,array,max_nodes_cross_section,(length_division*2+1)
  *do,j,1,(length_division*2+1)
    cmsel,s,winding%i%
    allsel,below,area
    vmin = (j-1)*length/(2*length_division)-tolerance
    vmax = (j-1)*length/(2*length_division)+tolerance
    nsel,r,loc,x,vmin,vmax
    *vget,node_list_winding_%i%(1,%j%),node,,nlist
  *enddo
  *mwrite,node_list_winding_%i%,Winding_%i%,txt
  (201E20.10)
*enddo

csys,0
allsel
*get,nnum_windings,node,,count
*dim,node_loc_windings,array,nnum_windings,4
*vget,node_loc_windings(1,1),node,,nlist
*vget,node_loc_windings(1,2),node,,loc,x
*vget,node_loc_windings(1,3),node,,loc,y
*vget,node_loc_windings(1,4),node,,loc,z

*mwrite,node_loc_windings,Node_Position,txt
(4E20.10)

















! *del,node_number_list
