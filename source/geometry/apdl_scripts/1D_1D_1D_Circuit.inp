
! input from Python
! resistance_dump
! inductance_init
! current_init

r_switch1 = 1e12

cmsel,s,winding1
allsel,below,elem
*get,first_node,node,,num,min

cmsel,s,winding%number_of_windings%
allsel,below,elem
*get,last_node,node,,num,max

csys,0
*get,first_kp_x,node,first_node,loc,x
*get,first_kp_y,node,first_node,loc,y
*get,first_kp_z,node,first_node,loc,z

*get,last_kp_x,node,last_node,loc,x
*get,last_kp_y,node,last_node,loc,y
*get,last_kp_z,node,last_node,loc,z

! create additional nodes for an electric circuit
/prep7
allsel
*get,last_node_magnet,node,,count
circ_node = last_node_magnet+1

n,circ_node,first_kp_x,first_kp_y,first_kp_z-0.25
n,circ_node+1,last_kp_x,last_kp_y,last_kp_z-0.25

n,circ_node+2,first_kp_x,first_kp_y,first_kp_z-0.5
n,circ_node+3,last_kp_x,last_kp_y,last_kp_z-0.5

! dump resistor
*get,et_num,etyp,0,num,max
et_res_dump = et_num+10
et,et_res_dump,circu124
keyopt,et_res_dump,1,0
r,et_res_dump,resistance_dump
type,et_res_dump
real,et_res_dump
e,circ_node,first_node
allsel
*get,dump_res_elem,elem,,count

! inductor
et_inductor = et_num+11
et,et_inductor,circu124
keyopt,et_inductor,1,1
r,et_inductor,inductance_init,-current_init   
type,et_inductor
real,et_inductor
e,circ_node+1,last_node
allsel
*get,inductor_elem,elem,,count

! independent current source
et_curr_source = et_num+12
et,et_curr_source,circu124
keyopt,et_curr_source,1,3
keyopt,et_curr_source,2,4
infin_time = 10000000
r,et_curr_source,0,current_init,infin_time,current_init
type,et_curr_source
real,et_curr_source
e,circ_node+1,circ_node
allsel
*get,curr_source_elem,elem,,count

! switch resistor
et_switch_resistor = et_num+13
et,et_switch_resistor,circu124
et,et_switch_resistor,circu124
keyopt,et_switch_resistor,1,0
r,et_switch_resistor,r_switch1
type,et_switch_resistor
real,et_switch_resistor
e,circ_node+2,circ_node+3
allsel
*get,switch_res_elem,elem,,count

! initial voltage coupling to connect the circuit with the switch resistor
cp,next,volt,circ_node,circ_node+2
*get,cp_current_source1,cp,,max
cp,next,volt,circ_node+1,circ_node+3
*get,cp_current_source2,cp,,max

*cfopen,Process_Finished,txt
*vwrite,1
(1(ES16.7))
*cfclose

save