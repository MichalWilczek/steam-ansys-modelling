
Strand_diameter = 0.0007 ! m

pi = acos(-1)
Cu_density = 8960  ! kg/m3
G10_density = 1420 ! kg/m3

! value of magnetic field - constant
B_field = 2.88

! input parameters for material properties
iop = 150
bc20 = 14.5
tco = 9.2
c1 = 3449
c2 = -257
tc = tco*(1-B_field/bc20)**0.59
tcs = tc*(1-iop/(c1+c2*B_field))
RRR = 150
fcu = 0.62263
max_size = 500
*dim,Cu_resistivity,table,max_size,2,1,temp
*dim,Cu_conductivity,table,max_size,1,1,temp
*dim,Cu_cp,table,max_size,1,1,temp
*dim,Nb_Ti_cp,table,max_size,1,1,temp
*dim,Wire_cp,table,max_size,1,1,temp

!!!!!!!!!!!!!!!!!!!!!!!!!!!! Copper Resistivity !!!!!!!!!!!!!!!!!!!!!!!!!!!
! constants of the polynomial
a0 = 1.7
a1 = 2.33e9
a2 = 9.57e5
a3 = 163

Cu_resistivity(0,1) = 0.0
*do,i,1,max_size,1
  Cu_resistivity(i,0) = i
  Cu_resistivity(i,1) = (a0/RRR+1/(a1/i**5+a2/i**3+a3/i))*10**(-8)+(0.37+0.0005*RRR)*B_field*10**(-10)
  Cu_resistivity(i,2) = 1.0e-15
*enddo

!!!!!!!!!!!!!!!!!!!!!!!!!!! Copper Thermal Conductivity !!!!!!!!!!!!!!!!!!!!
Cu_conductivity(0,1) = 0.0
*do,i,1,max_size,1
  Cu_conductivity(i,0) = i
  Cu_conductivity(i,1) = 2.45*10**(-8)*i/Cu_resistivity(i,1)
*enddo

!!!!!!!!!!!!!!!!!!!!!!!!!!!!! Copper Heat Capacity !!!!!!!!!!!!!!!!!!!!!!!!!!
Cu_cp(0,1) = 0.0
*do,i,1,max_size,1
  Cu_cp(i,0) = i
  *if,i,lt,10,then
    a = (-3.080E-02)*i**4
    b = (7.230E+00)*i**3
    c = (-2.129E+00)*i**2
    d = (1.019E+02)*i
    e = 2.563E+00

  *elseif,10,le,i,and,i,lt,40,then
    a = (-3.045E-01)*i**4
    b =  (2.987E+01)*i**3
    c = (-4.556E+02)*i**2
    d =  (3.470E+03)*i
    e = -8.250E+03

  *elseif,40,le,i,and,i,lt,125,then
    a = (4.190E-02)*i**4
    b = (-1.402E+01)*i**3
    c = (1.509E+03)*i**2
    d = (-3.160E+04)*i
    e = 1.784E+05

  *elseif,125,le,i,and,i,lt,300,then
    a = (-8.480E-04)*i**4
    b = (8.419E-01)*i**3
    c = (-3.255E+02)*i**2
    d = (6.059E+04)*i
    e = -1.290E+06

  *elseif,300,le,i,and,i,lt,500,then
    a = (-4.800E-05)*i**4
    b = (9.173E-02)*i**3
    c = (-6.412E+01)*i**2
    d = (2.036E+04)*i
    e = 1.030E+06

  *else
    a = (0.000E+00)*i**4
    b = (1.200E-05)*i**3
    c = (-2.149E-01)*i**2
    d = (1.004E+03)*i
    e = 3.180E+06
  *endif
    Cu_cp(i,1) = a+b+c+d+e
*enddo

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! NbTi Heat Capacity !!!!!!!!!!!!!!!!!!!!!!!!!
Nb_Ti_cp(0,dt) = 0.0
*do,i,1,max_size,1
  Nb_Ti_cp(i,0) = i
  *if,i,lt,tc,then
    a = (0.00000E+00)*i**4
    b = (4.91000E+01)*i**3
    c = (0.00000E+00)*i**2
    d = (6.40000E+01)*i
    e = 0.00000E+00

  *elseif,tc,le,i,and,i,lt,20,then
    a = (0.00000E+00)*i**4
    b =  (1.62400E+01)*i**3
    c = (0.00000E+00)*i**2
    d =  (9.28000E+02)*i
    e =  0.00000E+00

  *elseif,20,le,i,and,i,lt,50,then
    a = (-2.17700E-01)*i**4
    b = (1.19838E+01)*i**3
    c = (5.53710E+02)*i**2
    d = (-7.84610E+03)*i
    e = 4.13830E+04

  *elseif,50,le,i,and,i,lt,175,then
    a = (-4.82000E-03)*i**4
    b = (2.97600E+00)*i**3
    c = (-7.16300E+02)*i**2
    d = (8.30220E+04)*i
    e = -1.53000E+06

  *elseif,175,le,i,and,i,lt,500,then
    a = (-6.29000E-05)*i**4
    b = (9.29600E-02)*i**3
    c = (-5.16600E+01)*i**2
    d = (1.37060E+04)*i
    e = 1.24000E+06

  *else
    a = (0.00000E+00)*i**4
    b = (0.00000E+00)*i**3
    c = (-2.57000E-01)*i**2
    d = (9.55500E+02)*i
    e = 2.45000E+06
  *endif
    Nb_Ti_cp(i,1) = a+b+c+d+e
*enddo

!!!!!!!!!!!!!!!!!!!!!!! Equivalent Heat Capacity !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
Wire_cp(0,1) = 0.0
*do,i,1,max_size,1
  Wire_cp(i,0) = i
  Wire_cp(i,1) = (Cu_cp(i,1)+(1/fcu-1)*Nb_Ti_cp(i,1))/Cu_density
*enddo

!!!!!!!!!!!!!!!!!!!!!! Kapton Thermal Conductivity !!!!!!!!!!!!!!!!!!!!!!!!!!!
*dim,kkap,table,max_size,1,1,temp
kkap(0,1) = 0.0
*do,i,1,max_size,1
  t = i*0.1
  kkap(i,0) = t
  *if,t,lt,4.3,then
    kkap(i,1) = 0.00378 + 0.00161*t
  *else
    a = 5.73101
    b = -39.511*log10(t)
    c = 79.9313*log10(t)**2
    d = -83.8572*log10(t)**3
    e = 50.9157*log10(t)**4
    f = -17.9835*log10(t)**5
    g = 3.42413*log10(t)**6
    h = -0.27133*log10(t)**7
    kkap(i,1) = 10**(a+b+c+d+e+f+g+h)
  *endif
*enddo

!!!!!!!!!!!!!!!!!!!!!! Kapton Heat Capacity !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
*dim,cpkap,table,max_size,1,1,temp
cpkap(0,1) = 0.0
*do,i,1,max_size,1
  t = i*0.1
  cpkap(i,0) = t
    a = -1.3684
    b = 0.65892*log10(t)
    c = 2.8719*log10(t)**2
    d = 0.42651*log10(t)**3
    e = -3.0088*log10(t)**4
    f = 1.9558*log10(t)**5
    g = -0.51998*log10(t)**6
    h = 0.051574*log10(t)**7
  cpkap(i,1) = 10**(a+b+c+d+e+f+g+h)
*enddo

!!!!!!!!!!!!!!!!!!!!!! Material Definition !!!!!!!!!!!!!!!!!!!!!!

! element 1 - quenched zone
! element 2 - superconducting zone, no heat generation
! element 3 - insulation

et,1,link68     ! thermo-electric element
et,2,link68     ! thermo-electric element
et,3,link33     ! thermal 1d conduction element

pi = acos(-1)
Strand_area = (pi/4)*Strand_diameter**2

r,1,Strand_area
r,2,Strand_area
r,3,G10_element_area

! keyopt,1,1,110  ! thermo-electric analysis, dof: temp, volt
! keyopt,1,10,0   ! consisent heat matrix, 1-diagonalised, 2-diagonalised, temp-dep specific heat or enthalpy is evaluated at the element centroid

! keyopt,2,1,110  ! thermo-electric analysis, dof: temp, volt
! keyopt,2,10,0   ! consisent heat matrix, 1-diagonalised, 2-diagonalised, temp-dep specific heat or enthalpy is evaluated at the element centroid

mp,dens,1,Cu_density ! to be changed
mp,dens,2,Cu_density ! to be changed
mp,dens,3,G10_density

*do,i,1,100,1
  mptemp,i,i
  mpdata,kxx,1,,Cu_conductivity(i,1)
  ! mpdata,kyy,1,,Cu_conductivity(i,1)
  mpdata,kxx,2,,Cu_conductivity(i,1)
  ! mpdata,kyy,2,,Cu_conductivity(i,1)
  mpdata,kxx,3,,kkap(i,1)
*enddo

*do,i,1,100,1
  mptemp,i,i
  mpdata,c,1,,Wire_cp(i,1)
  mpdata,c,2,,Wire_cp(i,1)
  mpdata,c,3,,cpkap(i,1)  ! heat capacity of G10 (set constant by now)
*enddo

*do,i,1,100,1
  mptemp,i,i
  mpdata,rsvx,1,,Cu_resistivity(i,1)
  ! mpdata,rsvy,1,,Cu_resistivity(i,1)
  mpdata,rsvx,2,,1.0e-16 ! RESISTIITY OF NON-QUENCHED ZONE
  ! mpdata,rsvy,2,,1.0e-16 ! RESISTIITY OF NON-QUENCHED ZONE
*enddo

! if file input finished, create file - for communication with Python
*cfopen,Process_Finished,txt
*vwrite,1
(1(ES16.7))
*cfclose
