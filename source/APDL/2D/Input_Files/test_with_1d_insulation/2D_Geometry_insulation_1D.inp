
! geometry creation
x=0
y=0
z=0
*do,i,1,number_of_windings
  allsel
  *get,knum,kp,,count
  k,knum+1,x,y,z
  k,knum+2,x+length_per_winding,y,z
  k,knum+3,x+length_per_winding,y+transverse_dimension_winding,z
  k,knum+4,x,y+transverse_dimension_winding,z
  l,knum+1,knum+2,division_per_winding
  l,knum+2,knum+3,transverse_division_winding
  l,knum+3,knum+4,division_per_winding
  l,knum+4,knum+1,transverse_division_winding
  *get,lnum,line,,count
  lsel,s,line,,lnum-3,lnum
  al,all
  allsel
  *get,anum,area,,count
  asel,s,area,,anum
  cm,winding%i%,area
  mshape,0,2D
  mshkey,1
  type,2
  real,2
  mat,2
  amesh,all

  allsel
  *get,knum,kp,,count
  k,knum+1,x,y+transverse_dimension_winding,z
  k,knum+2,x+length_per_winding,y+transverse_dimension_winding,z
  k,knum+3,x+length_per_winding,y+transverse_dimension_winding+transverse_dimension_insulation,z
  k,knum+4,x,y+transverse_dimension_winding+transverse_dimension_insulation,z
  l,knum+1,knum+2,division_per_winding
  l,knum+2,knum+3,transverse_division_insulation
  l,knum+3,knum+4,division_per_winding
  l,knum+4,knum+1,transverse_division_insulation
  *get,lnum,line,,count
  lsel,s,line,,lnum-3,lnum
  al,all
  allsel
  *get,anum,area,,count
  asel,s,area,,anum
  cm,insulation%i%,area
  mshape,0,2D
  mshkey,1
  type,3
  real,3
  mat,3
  amesh,all

  x = x
  y = y + transverse_dimension_winding + transverse_dimension_insulation
  z = z
*enddo

! set tolerance for finding nodes
tolerance = (length_per_winding/(division_per_winding*2))/10

! to be reviewed
allsel
nummrg,node

*do,i,1,number_of_windings
  *dim,node_list_winding_%i%,array,winding_plane_max_number_nodes,(division_per_winding*2+1)
  *do,j,1,(division_per_winding*2+1)
    cmsel,s,winding%i%
    allsel,below,area
    vmin = (j-1)*length_per_winding/(2*division_per_winding)-tolerance
    vmax = (j-1)*length_per_winding/(2*division_per_winding)+tolerance
    nsel,r,loc,x,vmin,vmax
    *vget,node_list_winding_%i%(1,%j%),node,,nlist
  *enddo
  *mwrite,node_list_winding_%i%,Winding_%i%,txt
  (1001E20.10)
  !needs to be changed (automated) at some point
*enddo

allsel
*get,nnode,node,,count
*cfopen,Nnode,txt
*vwrite,nnode
(1(ES16.7))
*cfclose

csys,0
allsel
*get,nnum_windings,node,,count
*dim,node_loc_windings,array,nnum_windings,4
*vget,node_loc_windings(1,1),node,,nlist
*vget,node_loc_windings(1,2),node,,loc,x
*vget,node_loc_windings(1,3),node,,loc,y
*vget,node_loc_windings(1,4),node,,loc,z

*mwrite,node_loc_windings,Node_Position,txt
(4E20.10)

*cfopen,Process_Finished,txt
*vwrite,1
(1(ES16.7))
*cfclose

! *del,node_number_list
